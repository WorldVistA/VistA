ZZRGUTEX ;RGI/CBR,KW/JPS - Unit Tests - RPC Broker ;12/05/2014
 ;;1.0;UNIT TEST;;Apr 25, 2012;Build 1
 TSTART
 I $T(EN^%ut)'="" D EN^%ut("ZZRGUTEX")
 TROLLBACK
 Q
 ;
STARTUP ;
 N GMPIFN1,GMPIFN2
 S DTIME=500 ;D INIT^GMPLMGR
 K XUSER,XOPT
 S DUZ=$$CHECKAV^XUSRB("fakedoc1;1Doc!@#$")
 S GMPLUSER=1
 D DUZ^XUS1A
 S GMPVAMC=DUZ(2)
 S GMPROV=DUZ
 S DT=$P($$HTFM^XLFDT($H),".")
 S U="^"
 S GMPCLIN=$O(^SC("B","VISTA HEALTH CARE",""))_"^VISTA HEALTH CARE"
 D NEWPAT
 Q
 ;
SHUTDOWN ;
 Q
 ;
NEWPAT ;
 N PAT,PATU,PATDFN,ERR
 S PAT(.01)="GMPLTEST, PATIENT"
 S PAT(.02)="M"
 S PAT(.03)=2820226
 S PAT(.09)="000000121"
 S PAT(.096)=1
 S PAT(.097)=$P($$HTFM^XLFDT($H),".")
 S PAT(.301)="Y"
 S PAT(391)=1
 S PAT(1901)="Y"
 M PATU(2,"+1,")=PAT
 D UPDATE^DIE("","PATU","PATDFN","ERR")
 S GMPDFN=+$G(PATDFN(1))
 Q
 ;
MKPROB ; Add problem
 N PL
 S ACTDIA="872^253.2"
 S GMPNARR="Hypopituitarism (Diabetes Insipidus)"
 S GMPONS="3120101"
 S GMPRP=1
 S PL("DIAGNOSIS")=ACTDIA
 S PL("PATIENT")=GMPDFN
 S PL("NARRATIVE")=GMPNARR
 S PL("STATUS")="A"
 S PL("ONSET")=GMPONS
 S PL("LEXICON")="304837"
 S PL("PROVIDER")=GMPRP
 S PL("LOCATION")=GMPCLIN
 S PL("RECORDED")="3120202"
 D CREATE^GMPLUTL(.PL,.GMPIFN1)
 Q
 ;
PROBNAR ;
 N %,PROB1,PROB2
 D MKPROB
 I GMPIFN1'>0 D FAIL^%ut("Save failed: "_$G(GMPIFN1(0))) Q
 S %=$$PROBNAR^IBDFBK3(GMPIFN1)
 D CHKEQ^%ut($P($G(^AUTNPOV(+$P($G(^AUPNPROB(+$G(GMPIFN1),0)),"^",5),0)),"^"),%)
 Q
 ;
PROBDIA ;
 N %
 S %=$$PROBDIA^IBDFBK3(GMPIFN1)
 D CHKEQ^%ut(+$P($G(^AUPNPROB(+$G(GMPIFN1),0)),"^"),%)
 Q
 ;
RORLO ;
 N ROR
 S RORTMP=$$ALLOC^RORTMP()
 S RORSTDT="3120101",RORENDT=$$NOW^XLFDT
 S %=$$LOAD^RORHL17(.ROR,GMPIFN1)
 D CHKEQ^%ut(0,%)
 D CHKEQ^%ut("P",ROR("OBR","COND"))
 D CHKEQ^%ut($$HL7DATE($$NOW^XLFDT),ROR("OBR","DE"),"DATE ENTERED")
 D CHKTF^%ut(ROR("OBR","DIAG")[$P(ACTDIA,U,2),"ICD9")
 D CHKEQ^%ut($$HL7DATE($$NOW^XLFDT),ROR("OBR","DLM"),"DATE LAST MODIFIED")
 D CHKEQ^%ut($$HL7DATE(GMPONS),ROR("OBR","DOO"),"ONSET")
 D CHKEQ^%ut($$HL7DATE("3120202"),ROR("OBR","DREC"),"DATE RECORDED")
 D CHKEQ^%ut($P($G(^AUPNPROB(GMPIFN1,0)),U,6),ROR("OBR","FACIL"),"FACILITY")
 D CHKEQ^%ut(GMPRP,ROR("OBR","RP"),"RECORDING PROVIDER")
 D CHKEQ^%ut("A",ROR("OBR","ST"),"STATUS")
 D CHKEQ^%ut(GMPNARR,ROR("OBX","PR"),"NARARTIVE")
 D CHKEQ^%ut(GMPNARR,ROR("OBX","PROB"),"PROBLEM")
 Q
 ;
NEPROB ;
 N %
 S %=$$NEPROB^PXRMISE()
 D CHKEQ^%ut($P(^AUPNPROB(0),U,4),%)
 Q
 ;
PXRMOUT ;
 N PXRMRM,OCC,IFI,TEXT,NARRLOC
 S PXRMRM=0
 S OCC(1)=""
 S IFI(1,"DATE")=$$NOW^XLFDT
 S IFI(1,"DAS")=GMPIFN1
 S IFI(1,"CODEP")=$P(ACTDIA,U,2)
 ; Additional information needed for the
 ; Nov 2014 FOIA release PXRM*2.0*26
 S IFI(1,"CODE")=$P(ACTDIA,U,2)
 S IFI(1,"CODESYS")="ICD"
 S IFI(1,"DATE ENTERED")="3031211"
 S IFI(1,"DATE LAST MODIFIED")="3041111"
 S IFI(1,"PROVIDER NARRATIVE")=1
 ; End new code
 S IFI(1,"STATUS")="A"
 D OUTPUT^PXRMPROB(0,.OCC,.IFI,0,.TEXT)
 S NARRLOC=12
 I $G(TEXT(NARRLOC))=" Modified:" S NARRLOC=23
 D CHKTF^%ut($D(TEXT(NARRLOC)),"Provider narrative not defined")
 D CHKEQ^%ut(" Hypopituitarism",$G(TEXT(NARRLOC)))
 ; This information doesnt seem to exist in new output....
 I NARRLOC=12 D CHKEQ^%ut(" PANHYPOPITUITARISM",$G(TEXT(4)))
 Q
 ;
PXRMPRB ;
 N PXCA,PXCADT,PXCANPOV,PXCAPDX,PXCAPAT
 S PXCAPAT=+GMPDFN,PXCAPDX=""
 S PXCA("ENCOUNTER")=$$NOW^XLFDT
 S PXCA("PROBLEM",DUZ,1)=+ACTDIA_"^"_GMPONS_"^^^^^^^^"_GMPIFN1
 S PXCADT=$$NOW^XLFDT
 D PROBLEM^PXCAPL(.PXCA,0,0)
 D CHKTF^%ut($D(PXCA("ERROR"))=0,"Unexpected PXCA Errors")
 K PXCA
 S PXCA("PROBLEM",DUZ,1)=+ACTDIA_"^"_GMPONS_"^^^^^^^^"_999999999
 S PXCA("ENCOUNTER")=$$NOW^XLFDT
 D PROBLEM^PXCAPL(.PXCA,0,0)
 D CHKEQ^%ut("Problem not in file 9000011^999999999",$G(PXCA("ERROR","PROBLEM",DUZ,1,10)),"Expected: Problem not in file")
 S PXCAPAT=+GMPDFN+1
 K PXCA
 S PXCA("PROBLEM",DUZ,1)=+ACTDIA_"^"_GMPONS_"^^^^^^^^"_GMPIFN1
 S PXCA("ENCOUNTER")=$$NOW^XLFDT
 D PROBLEM^PXCAPL(.PXCA,0,0)
 D CHKEQ^%ut("Problem in file 9000011 is for a different Patient^"_+GMPIFN1,$G(PXCA("ERROR","PROBLEM",DUZ,1,10)),"Expected: problem is for a different patient")
 Q
 ;
PXDIAG ;
 N PXCA,PXCADT,PXCANPOV,PXCAPDX,PXCAPAT
 S PXCAPAT=+GMPDFN,PXCAPDX=""
 S PXCA("DIAGNOSIS",DUZ,1)=+ACTDIA_"^P^^^^^"_GMPIFN1_"^"_GMPNARR
 S PXCADT=$$NOW^XLFDT
 S PXCANPOV=0
 D DIAG^PXCAPOV(.PXCA,0,0)
 D CHKTF^%ut($D(PXCA("ERROR"))=0,"Unexpected PXCA Errors")
 K PXCA
 S PXCA("DIAGNOSIS",DUZ,1)=+ACTDIA_"^P^^^^^"_999999999_"^"_GMPNARR
 D DIAG^PXCAPOV(.PXCA,0,0)
 D CHKEQ^%ut("Problem not in file 9000011^999999999",$G(PXCA("ERROR","DIAGNOSIS",DUZ,1,7)),"Expected: Problem not in file")
 S PXCAPAT=+GMPDFN+1
 K PXCA
 S PXCA("DIAGNOSIS",DUZ,1)=+ACTDIA_"^P^^^^^"_GMPIFN1_"^"_GMPNARR
 D DIAG^PXCAPOV(.PXCA,0,0)
 D CHKEQ^%ut("Problem in file 9000011 is for a different Patient^"_+GMPIFN1,$G(PXCA("ERROR","DIAGNOSIS",DUZ,1,7)),"Expected: problem is for a different patient")
 Q
 ;
ACKQLST ;
 N %,PL,PLY
 S %=$$PLIST^ACKQUTL6(0,0) ;Invalid patient and/or diagnosis
 D CHKEQ^%ut(0,%)
 S %=$$PLIST^ACKQUTL6(GMPDFN,0) ;Invalid patient and/or diagnosis
 D CHKEQ^%ut(0,%)
 S %=$$PLIST^ACKQUTL6(0,0) ;Invalid patient and/or diagnosis
 D CHKEQ^%ut(0,%)
 S %=$$PLIST^ACKQUTL6(GMPDFN,+ACTDIA) ;ACTIVE problem
 D CHKEQ^%ut("2^"_GMPIFN1,%,"ACTIVE")
 S PL("PROBLEM")=GMPIFN1
 S PL("PROVIDER")=DUZ
 S PL("STATUS")="I"
 D UPDATE^GMPLUTL(.PL,.PLY) ; INACTIVATE PROBLEM
 S %=$$PLIST^ACKQUTL6(GMPDFN,+ACTDIA) ;INACTIVE problem
 D CHKEQ^%ut("1^"_GMPIFN1,%,"INACTIVE")
 Q
 ;
HL7DATE(HDT) ;
 Q (17+$E(HDT))_$E(HDT,2,7)
XTENT ;
 ;;PROBNAR
 ;;PROBDIA
 ;;RORLO
 ;;NEPROB
 ;;PXRMOUT
 ;;PXRMPRB
 ;;PXDIAG
 ;;ACKQLST
 Q
 ;
